 TopicConsumer;config;consumer;start;cr
⍝ A consumer consume all messages from two different topics

 #.Init'.'

⍝ Consumer configurations
 config←0 2⍴⍬
 config⍪←'bootstrap.servers' 'localhost:9092'
 config⍪←'client.id' 'consumerTclient'
 config⍪←'group.id' 'consumerTgroup'
 config⍪←'auto.offset.reset' 'earliest'
 ⎕←'Init new consumer with config:'
 config
 consumer←⎕NEW #.Consumer config

⍝ Subscribe consumer to topic1 and topic2
 ⎕←'Subscribe consumer on topic "topic1" and "topic2":'
 consumer.subscribe'topic1' 'topic2'
 ⎕←'Waiting for rebalance before starting consuming...'
 ⎕DL 5

⍝ Consume messages
 ⎕←'Consume first message:'
 consumer.consume
 ⎕←'Consume the queue:'
 start←3⊃⎕AI
 :While 1
     cr←consumer.consume_record
     :If 1=⊃cr
     :AndIf 20000>(3⊃⎕AI)-start
         :Continue
     :ElseIf 0=⊃cr
         (2⊃cr).(Topic Payload Key Partition)
         start←0
     :Else
         cr
         :Leave
     :EndIf
 :EndWhile
 ⎕←'Close consumer:'
 ⎕EX'consumer'
