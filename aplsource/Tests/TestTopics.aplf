 TestTopics;i;config;producer;err;consumer;start;msgs;produced_msgs;consumed_msgs;C;P;sort_by_key;cr;t_consumed_msgs;t_produced_msgs;s_produced_msgs;s_consumed_msgs;_
⍝ Produce 15 msgs on topic1 and 20 on topic2
⍝ Consume all the msgs in the queue and check that
⍝ the consumed msgs are the same and
⍝ in the same order within each key.

 #.Init'.'

⍝ Consumer configurations
 config←0 2⍴⍬
 config⍪←'bootstrap.servers' 'localhost:9092'
 config⍪←'client.id' 'consumerclient'
 config⍪←'group.id' 'consumergroup'
 config⍪←'auto.offset.reset' 'earliest'
 ⎕←'Consumer configuration'
 config

⍝ Init new Consumer
 consumer←⎕NEW #.Consumer config

⍝ Subscribe consumer to topic
 ⎕←'Subscribe consumer to topics topic1 and topic2'
 consumer.subscribe'topic1' 'topic2'

 ⎕←'Consume the queue before producing messages for testing'
 start←3⊃⎕AI
 consumed_msgs←⍬
 :While 1
     cr←consumer.consume_record
     :If 1=⊃cr
     :AndIf 20000>(3⊃⎕AI)-start
         :Continue
     :ElseIf 0=⊃cr
         _←⊂(2⊃cr).(Topic Payload Key Partition)
         start←0
     :Else
         cr
         :Leave
     :EndIf
 :EndWhile

⍝ Producer configurations
 config←0 2⍴⍬
 config⍪←'bootstrap.servers' 'localhost:9092'
 config⍪←'client.id' 'producerclient'
 ⎕←'Producer configuration'
 config

⍝ Init new Producer
 producer←⎕NEW #.Producer config

⍝ Produce bundled messages on topic and ask for dr
 produced_msgs←⍬
 :For i :In ⍳15
     msgs←'topic1'('payload',⍕i)('key',⍕5|i)
     producer.produce_record ⎕NEW #.Record(msgs)
     produced_msgs,←⊂msgs
     :If 0=10|i
         ⎕←'Delivery report:'
         producer.update_outstanding
     :EndIf
 :EndFor

 :For i :In ⍳20
     msgs←'topic2'('payload',⍕i)('key',⍕5|i)
     producer.produce_record ⎕NEW #.Record(msgs)
     produced_msgs,←⊂msgs
     :If 0=10|i
         ⎕←'Delivery report:'
         producer.update_outstanding
     :EndIf
 :EndFor

 ⎕EX'producer'

 ⎕←'Now consume the testing queue'
 start←3⊃⎕AI
 consumed_msgs←⍬
 :While 1
     cr←consumer.consume_record
     :If 1=⊃cr
     :AndIf 20000>(3⊃⎕AI)-start
         :Continue
     :ElseIf 0=⊃cr
         consumed_msgs,←⊂(2⊃cr).(Topic Payload Key Partition)
         start←0
     :Else
         cr
         :Leave
     :EndIf
 :EndWhile

 ⎕EX'consumer'

 ⍝ Sort first by topic and then by key. The order is guaranteed in key
 t_consumed_msgs ← 1{⍵[⍋⍵[;⍺];]}↑¯1↓¨consumed_msgs
 s_consumed_msgs ← 3{⍵[⍋⍵[;⍺];]}t_consumed_msgs

 t_produced_msgs ← 1{⍵[⍋⍵[;⍺];]}↑produced_msgs
 s_produced_msgs ← 3{⍵[⍋⍵[;⍺];]}t_produced_msgs

'Produced and consumed same messages in the correct order from two topics' ASSERT s_consumed_msgs ≡ s_produced_msgs
